#!/usr/bin/env zsh
#---------------------------------------------------------
#
# Build remastered Arch ISO
#
# @author   TADOKORO Saneyuki <saneyan@mail.gfunction.com>
# @license  MIT License
#
#---------------------------------------------------------

declare -r _dirname=$(cd $(dirname $0)/../ && pwd)

source $_dirname/subsets/remaster-env
source $_dirname/subsets/remaster-utils
source $_dirname/subsets/remaster-aliases
source $_dirname/subsets/bind-build

enforce-survey "You must deploy the environment."

__ "Start building remastered Arch Linux..."

cd $_in/archive/arch/$_arch

if [ $_aufs = true ]; then
  mount-aufs $_aufs_r_work $_dirname $_in/mnt/remaster
else
  sudo ar-cpr $_dirname $_in/mnt/remaster
fi

#----------------------------
__ ":: Mounting cookbooks..."
#----------------------------
if [[ -n $_cookbook ]]; then
  if [ $_aufs = true ]; then
    mount-aufs $_aufs_c_work $_cookbook $_in/mnt/cookbooks
  else
    sudo ar-cpr $_cookbook $_in/mnt/cookbooks
  fi
else
  echo "No cookbooks specified."
fi

#-------------------------
__ ":: Entering chroot..."
#-------------------------
if [ $_auto = true ]; then
  [ -e $_in/mnt$_defaultz ] && sudo ar-mv $_in/mnt$_defaultz $_in/mnt$_afterz
  sudo ar-cp $_dirname/chroot/bootstrap.zsh $_in/mnt$_defaultz
  sudo DEFAULTZ=$_defaultz AFTERZ=$_afterz UPDATE=$_update ARCH=$_arch arch-chroot $_in/mnt /bin/zsh
else
  echo "Let's customize your Arch Linux. Enjoy!"
  sudo arch-chroot $_in/mnt $_shell
fi

__ "Exit chroot"

if [[ -n $_cookbook ]]; then
  if [ $_aufs = true ]; then
    umount-aufs $_aufs_c_work $_in/mnt/cookbooks
  else
    sudo ar-rmr $_in/mnt/cookbooks
  fi 
fi

if [ $_aufs = true ]; then
  umount-aufs $_aufs_r_work $_in/mnt/remaster
else
  sudo ar-rmr $_in/mnt/remaster
fi

if [ $_squash = true ]; then
  #-------------------------
  __ ":: Making squashfs..."
  #-------------------------
  ar-cp $_in/mnt/boot/vmlinuz-linux $_in/archive/arch/boot/$_arch/vmlinuz
  ar-cp $_in/mnt/boot/initramfs-linux.img $_in/archive/arch/boot/$_arch/archiso.img
  sudo ar-mv $_in/mnt/pkglist.txt $_in/archive/arch/pkglist.$_arch.txt
  sudo ar-rm airootfs.sfs
  sudo mksquashfs $_in/squashfs-root airootfs.sfs

  #------------------------
  __ ":: Updating files..."
  #------------------------
  sudo md5sum airootfs.sfs > airootfs.md5
fi

if [ $_generate = true ]; then
  #----------------------------------------
  __ ":: Generating remastered Arch ISO..."
  #----------------------------------------
  xorriso -as mkisofs \
    -iso-level 3 \
    -full-iso9660-filenames \
    -volid 'ARCH_201410' \
    -eltorito-boot isolinux/isolinux.bin \
    -eltorito-catalog isolinux/boot.cat \
    -no-emul-boot -boot-load-size 4 -boot-info-table \
    -isohybrid-mbr $_in/archive/isolinux/isohdpfx.bin \
    -output $_out \
    $_in/archive
fi

[ $? = 0 ] && echo "Done!"
